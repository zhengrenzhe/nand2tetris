class Interactive {
    // for save user input
    static LinkList list;

    // for save all numbers
    static LinkList number_list;

    // for save + - * /
    static LinkList operator_list;

    function void init() {
        let list = LinkList.new();
        let number_list = LinkList.new();
        let operator_list = LinkList.new();
        return;
    }

    // remove all values in list
    function void clear() {
        do list.clear();
        do number_list.clear();
        do operator_list.clear();
        do Display.refresh(list);
        return;
    }

    function void calculateResult() {
        var int result;

        do Interactive.parseUserInput();
        // remove = in operator_list
        do operator_list.popTail();

        // first calculate multiply and divide, high priority 
        do Interactive.calculate(42, 47);
        // second calculate addition and subtraction, low priority 
        do Interactive.calculate(43, 45);

        // only one item in number_list, indicate the calculate is successfully.
        if (number_list.getLength() = 1) {
            let result = number_list.getNodeValueByIndex(0);
            // clear lists
            do Interactive.clear();
            // display the result
            do Display.show(result);
        }else{
            do Output.printString("your input expression maybe something wrong");
            do Output.println();
            do Output.printString("need press AC (ESC button)");
        }
        return;
    }

    /**
    calculate number_list and operator_list
    traverse iterator list, pop two operands when encountering a symbol
    at the end of the calculation, replace the first operand with the result of the calculation and remove the second operand from the number_list
    for example:
        number_list: [1, 2]
        operator_list: ['+']
    
    result:
        number_list: [3]
        operator_list: []
    */
    function void calculate(int opTypeA, int opTypeB) {
        var Node opHead, opCurrent, numberNodeA, numberNodeB;
        var int opIndex, currentOpResult;

        let opHead = operator_list.getHead();
        let opCurrent = opHead.getNext();
        let opIndex = 0;

        while(~(opCurrent.getData() = null)){
            if ((opCurrent.getData() = opTypeA) | (opCurrent.getData() = opTypeB)) {
                let numberNodeA = number_list.getNodeByIndex(opIndex);
                let numberNodeB = numberNodeA.getNext();

                if (opCurrent.getData() = 42) {
                    let currentOpResult = Math.multiply(numberNodeA.getData(), numberNodeB.getData());
                }
                if (opCurrent.getData() = 47){
                    let currentOpResult = Math.divide(numberNodeA.getData(), numberNodeB.getData());
                }
                if (opCurrent.getData() = 43){
                    let currentOpResult = numberNodeA.getData() + numberNodeB.getData();
                }
                if (opCurrent.getData() = 45){
                    let currentOpResult = numberNodeA.getData() - numberNodeB.getData();
                }

                do number_list.replaceData(opIndex, currentOpResult);
                do number_list.removeNodeByIndex(opIndex + 1);
                do operator_list.removeNodeByIndex(opIndex);

                let opHead = operator_list.getHead();
                let opCurrent = opHead.getNext();
                let opIndex = 0;
            }else{
                let opCurrent = opCurrent.getNext();
                let opIndex = opIndex + 1;
            }
        }

        return;
    }

    /**
    split character sequence to number_list and operator_list   
    for example:
        we got a character sequence by user input ['1', '2', '+', '3', '4']
        this function will merge '1' '2' as number 12, '3' '4' as number 34
        the number_list will got [12, 34]
        the operator_list will got ['+']
    */
    function void parseUserInput() {
        var Node head, current;
        var Array tmp;
        var int index, tmpIndex, number;
        
        let head = list.getHead();
        let current = head.getNext();
        let tmp = Array.new(20);
        let index = 0;

        while (~(current.getData() = null)) {
            // is number
            if ((current.getData() > 47) & (current.getData() < 58)) {
                let tmp[index] = current.getData();
                let index = index + 1;
            
            // is symbol
            }else{
                // the numbers(character) before the symbol
                let tmpIndex = 0;
                while (tmpIndex < index){
                    let number = number + ((tmp[tmpIndex] - 48) * MathMore.Exp(10, index - tmpIndex - 1));
                    let tmpIndex  = tmpIndex + 1;
                }

                do number_list.pushTail(number);
                do operator_list.pushTail(current.getData());

                let index = 0;
                let number = 0;
            }
            let current = current.getNext();
        }

        return;
    }

    function void onKeyPress(char keyCode) {
        // from 0 to 9
        if (keyCode > 47){
            if (keyCode < 58){
                do list.pushTail(keyCode);
            }
        }

        // =
        if (keyCode = 61){
            do list.pushTail(keyCode);
            do Interactive.calculateResult();
            return;
        }
        
        // +
        if(keyCode = 43){
            do list.pushTail(keyCode);
        }

        // -
        if(keyCode = 45){
            do list.pushTail(keyCode);
        }

        // *
        if(keyCode = 42){
            do list.pushTail(keyCode);
        }

        // /
        if(keyCode = 47){
            do list.pushTail(keyCode);
        }

        // esc for ac button
        if(keyCode = 140){
            do Interactive.clear();
        }

        do Display.refresh(list);

        return;
    }
}